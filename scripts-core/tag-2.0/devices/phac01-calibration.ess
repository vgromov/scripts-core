// phac01-calibration script
##require("devices/EsDeviceBase.ess");
##require("phac01.ess");

function phac01create(fwId)
{
  if( EsDeviceType$$PHAC1 == fwId$devType )
    return new PHAC01(fwId);  
}

function phac01opmodeCheck(device)
var result;
{
  result = device.rpcCall(PHAC01_Rpid$$WorkmodeGet, null);
  if( result[0] )
  {
    if( !(result[1] in [PHAC01_OpMode$$Illuminance .. PHAC01_OpMode$$Luminance]) )
      result = device.rpcCall(PHAC01_Rpid$$WorkmodeSet, PHAC01_OpMode$$Illuminance);
    
    return result[0];
  }
  
  return false;  
}

function phac01stateBatchPrepare()
var batch = [], item = [];
{
  // request entire state info
  item += EsRpcStdId$$HEALTH_INFO_GET;
  item += null;
  batch#append(item); item = [];

  item += EsRpcStdId$$DATETIME_GET;
  item += null;
  batch#append(item); item = [];

  item += EsRpcStdId$$POWER_STATUS_GET;
  item += null;
  batch#append(item);
  
  return batch;
}

function phac01stateRequest(device)
var result;
{
  return device.rpcBatchCall( phac01stateBatchPrepare() );
}

function phac01liveRequest(device)
var batch = phac01stateBatchPrepare(), item = [], result;
{
  item += PHAC01_Rpid$$LiveDataGet;
  item += null;
  batch#append(item);
  
  return device.rpcBatchCall(batch);
}

/// Reference profile node
object PHAC01_CalibrationProfileNode
{
  var m_ref, m_deviation,
    m_refCalibrated, m_liveVerified;
    
  function compare(other)
  {
    if( !other#isEmpty() )
    {
      if( !other#isKindOf("PHAC01_CalibrationProfileNode") )
        throw "Incompatible comparison argument type, must be 'PHAC01_CalibrationProfileNode'";
      
      if(m_ref == other.m_ref)
        return 0;
      else if(m_ref < other.m_ref)
        return -1;
    }
    
    return 1;
  }
    
  function refValidate(ref)
  {
    if( ref <= 0 )
      throw "Reference must be non-zero positive value";
  }
  
  function deviationValidate(deviation)
  {
    EsNumericCheck::checkRangeFloat(0, 1, deviation, "Reference deviation");
  }
    
  new(ref, deviation)
  {
    refValidate(ref);
    m_ref = ref#asDouble();
    deviationValidate(deviation);
    m_deviation = deviation#asDouble();
  }

  /// Custom stream writer
  function streamWrite(stream)
  {
    stream.propertiesWrite( this );
  }
  
  /// Custom stream reader
  function streamRead(stream)
  {
    stream.propertiesRead( this );
  }    
 
  /// Reference node value
  property ref;
  @persistent;
  read: { return m_ref; }
  write: { refValidate(__value); m_ref = __value#asDouble(); }
  /// reference formatted string
  property refStr;
  read: { return PHAC01_ValueFmt( m_ref ); }    
  
  /// reference node deviation ratio, 0..1 (0..100%)
  property deviation;
  @persistent;
  read: { return m_deviation; }
  write: { deviationValidate(__value); m_deviation = __value#asDouble(); }
  /// Reference node deviation ratio, 0..1 (0..100%)
  property deviationStr;
  read: { return EsStr::format("%0.1f %%", m_deviation*100.); }
  
  /// Reference node calculated minimum
  property refMin;
  read: { return m_ref*(1.-m_deviation); }
  /// Reference node calculated minimum string
  property refMinStr;
  read: { return PHAC01_ValueFmt( $refMin ); }
  
  /// Reference node calculated maximum
  property refMax;
  read: { return m_ref*(1.+m_deviation); }
  /// Reference node calculated maximum string
  property refMaxStr;
  read: { return PHAC01_ValueFmt( $refMax ); }
  
  /// Live calibrated node
  property calibrated;
  @persistent;
  read: { return m_refCalibrated; }
  write: { 
    if(__value#isEmpty()) 
      m_refCalibrated = __value; 
    else
      m_refCalibrated = __value#asDouble();
  }
  /// Live calibrated node string
  property calibratedStr;
  read: { 
    if(m_refCalibrated#isEmpty()) 
      return "";
    else 
      PHAC01_ValueFmt(m_refCalibrated); 
  }
  /// Calibration validity check. If m_refCalibrated is not empty,
  /// and is more than 2 times greater|smaller than ref, false is returned
  property calibrationOutOfRange;
  read: {
    if( !m_refCalibrated#isEmpty())
    {
      return  2. < m_ref / m_refCalibrated ||
        m_refCalibrated / m_ref > 2.;
    }
    
    return false;
  }
  
  /// Live verified node
  property verified;
  @persistent;
  read: { return m_liveVerified; }
  write: { m_liveVerified = __value;}
  /// Live verified node string
  property verifiedStr;
  read: { 
    if(m_liveVerified#isEmpty()) 
      return "";
    else 
      PHAC01_ValueFmt(m_liveVerified); 
  }
}

/// Nodes collection index
enum PHAC01_CalibrationTypeIndex {
  Illuminosity   = 1, "Illuminosity";
  Luminosity     = 2, "Luminosity";
  Pulsation      = 3, "Pulsation";
}

function phac01CollectionIndexCheck(collection)
{
  if( !(collection in PHAC01_CalibrationTypeIndex) )
    throw "Collection index parameter is out of bounds";
}

function phac01UnitsGet(collection)
{
  phac01CollectionIndexCheck(collection);
  if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
    return "lx";
  else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
    return "cd/m2";
  else
    return "%";
}  

/// Reference profile.
object PHAC01_CalibrationProfile
{
  var m_name, m_lux, m_cdm, m_kp;
  
  function nodesClearAll()
  {
    m_lux = [];
    m_cdm = [];
    m_kp = [];
  }
  
  new(name)
  {
    m_name = name#asString();
    nodesClearAll();
  }
  
  function nodesCountGet(collection)
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      return m_lux#countGet();
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      return m_cdm#countGet();
    else
      return m_kp#countGet();
  }
  
  function nodesGet(collection)
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      return m_lux;
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      return m_cdm;
    else
      return m_kp;
  }
  
  function nodesCalibrationReset(collection)
  var node, nodes;
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      nodes = m_lux;
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      nodes = m_cdm;
    else
      nodes = m_kp;
    
    foreach(node in nodes)
    {
      node.m_liveVerified = null;
      node.m_refCalibrated = null;
    }
  }
  
  function hasCalibration(collection)
  var node, nodes;
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      nodes = m_lux;
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      nodes = m_cdm;
    else
      nodes = m_kp;

    foreach(node in nodes)
      if( !node.m_refCalibrated#isEmpty() )
        return true;
      
    return false;
  }
  
  function nodeGet(collection, idx)
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      return m_lux[idx];
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      return m_cdm[idx];
    else
      return m_kp[idx];
  }
  
  function nodeAdd(collection, ref, deviation)
  var newNode = new PHAC01_CalibrationProfileNode(ref, deviation);
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
    {
      m_lux#appendUnique(newNode);
      m_lux#sortAscending();
    }
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
    {
      m_cdm#appendUnique(newNode);
      m_cdm#sortAscending();
    }
    else
    {
      m_kp#appendUnique(newNode);
      m_kp#sortAscending();
    }
    
    return newNode;
  }
  
  function nodeDelete(collection, idx)
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      m_lux#itemDelete(idx);
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      m_cdm#itemDelete(idx);
    else
      m_kp#itemDelete(idx);
  }
  
  function nodeSet(collection, idx, ref, deviation)
  {
    nodeDelete(collection, idx);
    nodeAdd(collection, ref, deviation);
  }
  
  function nodesClear(collection)
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      m_lux = [];
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      m_cdm = [];
    else
      m_kp = [];
  }
  
/// Custom stream writer
  function streamWrite(stream)
  {    
    stream.valueWrite("name", m_name);
    stream.valueWrite("lux", m_lux);
    stream.valueWrite("cdm", m_cdm);
    stream.valueWrite("kp", m_kp);
  }
  
/// Custom stream reader
  function streamRead(stream)
  {
    m_name = stream.valueRead("name", "Profile");
    m_lux = stream.valueRead("lux", []);
    m_cdm = stream.valueRead("cdm", []);
    m_kp = stream.valueRead("kp", []);
  }

/// Properties
///
  property name;
  read: { return m_name; }
  write: { m_name = __value#asString(); }
  
  property isOk;
  read: { return 0 < m_lux#countGet() && 0 < m_cdm#countGet() && 0 < m_kp#countGet(); }
}

/// Session verification modes
enum PHAC01_SessionMode
{
  Verification   = 1, "Verification";
  Calibration   = 2, "Calibration";
  Complete      = 3, "Complete";
}

/// Calibration|verification session object
object PHAC01_CalibrationSession
{
  var 
  // Session creation timestamp 
    m_created,
  // Current session mode: verification|calibration|complete. Set to complete on session completion.
    m_mode, 
  // Completed variable holds completion timestamp, set automatically on session completion.
    m_complete,
  // Settings backup snapshot, taken upon session creation  
    m_bak,
  // Current settings state  
    m_cur,
  // Calibration profile used in session
    m_profile,
  // Temporary - calibration nodes unmatched against ref nodes in specific collection
    m_unmatchedLux,
    m_unmatchedCdm,
    m_unmatchedKp;
  
  // internal service - recalc raw value depending on collection index
  // i.e. for kp, we use tenths of percents
  function adaptVal(collection, rawVal)
  {
    if( PHAC01_CalibrationTypeIndex$$Pulsation == collection )
      return rawVal#asDouble() / 10.;
    
    return rawVal;
  }
  
  // update calibration marks for all ref collections, using m_cur 
  // as reference
  function calibrationMarksUpdate(collection)
  var calnodes, calnode, refnodes, refnode, 
    refFound, unmatched = [];
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
    {
      calnodes = m_cur.f_cal.f_lux;
      refnodes = m_profile.m_lux;
    }
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
    {
      calnodes = m_cur.f_cal.f_cdm;
      refnodes = m_profile.m_cdm;
    }
    else
    {
      calnodes = m_cur.f_cal.f_kp;
      refnodes = m_profile.m_kp;
    }
    
    foreach(calnode in calnodes)
    {
      if( calnode$notEmpty )
      {
        refFound = false;
        foreach(refnode in refnodes)
        {
          if( refnode$refMin <= adaptVal(collection, calnode.f_2) && refnode$refMax >= adaptVal(collection, calnode.f_2) )
          {
            refnode$calibrated = adaptVal(collection, calnode.f_1);
            refFound = true;
            break;
          }
        }
        
        if( !refFound )
          unmatched += calnode;
      }
    }
    
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      m_unmatchedLux = unmatched;
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      m_unmatchedCdm = unmatched;
    else
      m_unmatchedKp = unmatched;
  }
  
  new(mode, settingsBak, profile)
  var collection;
  {
    m_created = EsDateTime::now();
    m_mode = mode;
    m_bak = settingsBak;
    m_cur = settingsBak.clone();
    m_profile = profile.clone();
    foreach( collection in PHAC01_CalibrationTypeIndex )
      calibrationMarksUpdate(collection);
  }
  
/// Custom stream writer
  function streamWrite(stream)
  {
    stream.valueWrite("created", m_created);
    stream.valueWrite("mode", m_mode);
    stream.valueWrite("completed", m_complete);
    stream.valueWrite("bak", m_bak);
    stream.valueWrite("cur", m_cur);
    stream.valueWrite("profile", m_profile);
  }
  
/// Custom stream reader
  function streamRead(stream)
  {
    m_created = stream.valueRead("created", EsDateTime::now());
    m_mode = stream.valueRead("mode", PHAC01_SessionMode$$Verification);
    m_complete = stream.valueRead("completed", null);
    m_bak = stream.valueRead("bak", null);
    m_cur = stream.valueRead("cur", null);
    m_profile = stream.valueRead("profile", null);
  } 

/// Request calibration node assignment
  function calibrationSet(device, collection, val)
  var response, calnode = new PHAC01_CalNode(), args = [],
    calibration;
  {
    phac01CollectionIndexCheck(collection);
    calnode.f_1 = 0;
    calnode.f_2 = val;
    args += collection-1;
    args += calnode;
    response = device.rpcCall(PHAC01_Rpid$$CalibrationNodeSet, args);
    if( response[0] )
    {
      response = device.rpcCall(PHAC01_Rpid$$CalibrationGet, null);
      if( response[0] )
      {
        calibration = response[1];
        m_cur.f_cal.copyFrom( calibration );
        m_profile.nodesCalibrationReset(collection);
        calibrationMarksUpdate(collection);
        return true;
      }
    }
    
    return false;
  }

/// Request session calibration reset
  function calibrationReset(device, collection)
  var response;
  {
    phac01CollectionIndexCheck(collection);
    response = device.rpcCall(PHAC01_Rpid$$CalSelectiveReset, collection-1);
    if( response[0] )
    {
      response = device.rpcCall(PHAC01_Rpid$$CalibrationGet, null);
      if( response[0] )
      {
        m_cur.f_cal.copyFrom( response[1] );
        // reset all calibrated flags in profile at collection index
        m_profile.nodesCalibrationReset( collection );
        if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
          m_unmatchedLux = [];
        else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
          m_unmatchedCdm = [];
        else
          m_unmatchedKp = [];        
        return true;
      }
    }
    
    return false;
  }
  
/// Return true if device calibration may be reset for specified collection
  function calibrationCanReset(collection)
  {
    phac01CollectionIndexCheck(collection);
    return !m_cur.f_cal.checkEmpty(collection-1) || m_profile.hasCalibration(collection);
  }
  
/// Restore calibration backed up in session
  function calibrationRestore(device)
  var response, collection;
  {
    response = device.rpcCall(PHAC01_Rpid$$CalibrationSet, m_bak.f_cal);
    if( response[0] )
    {
      m_cur.f_cal.copyFrom( m_bak.f_cal );
      foreach(collection in PHAC01_CalibrationTypeIndex)
      {
        m_profile.nodesCalibrationReset(collection);
        calibrationMarksUpdate(collection);
      }
      
      return true;
    }
    
    return false;
  }

/// Return calibrated value for node
  function calibratedGet(collection, nodeIdx)
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      return m_profile.m_lux[nodeIdx]$calibrated;
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      return m_profile.m_cdm[nodeIdx]$calibrated;
    else
      return m_profile.m_kp[nodeIdx]$calibrated;
  }
  
/// Request verification set|reset
  function verificationSet(collection, nodeIdx, val)
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      m_profile.m_lux[nodeIdx]$verified = val;
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      m_profile.m_cdm[nodeIdx]$verified = val;
    else
      m_profile.m_kp[nodeIdx]$verified = val;
  }
  
/// Return verified value  for node
  function verificationGet(collection, nodeIdx)
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      return m_profile.m_lux[nodeIdx]$verified;
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      return m_profile.m_cdm[nodeIdx]$verified;
    else
      return m_profile.m_kp[nodeIdx]$verified;
  }

/// Look-up  reference node by value
/// Return -1 if not found, or ulong of found node
  function refNodeLookup(collection, liveVal)
  var node, nodes, result, idx = 0;
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      nodes = m_profile.m_lux;
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      nodes = m_profile.m_cdm;
    else
      nodes = m_profile.m_kp;
    
    foreach(node in nodes)
    {
      if( node$refMin <= liveVal && liveVal <= node$refMax )
        return idx;
      ++idx;
    }
    
    return -1;
  }
  
/// Return unmatched calibration nodes in collection
  function unmatchedGet(collection)
  {
    phac01CollectionIndexCheck(collection);
    if( PHAC01_CalibrationTypeIndex$$Illuminosity == collection )
      return m_unmatchedLux;
    else if( PHAC01_CalibrationTypeIndex$$Luminosity == collection )
      return m_unmatchedCdm;
    else
      return m_unmatchedKp;
  }
  
/// Properties
///  
  property created;
  read: { return m_created; }
  
  property isComplete;
  read: { return !m_complete#isEmpty() &&
          PHAC01_SessionMode$$Complete == m_mode; }
  
  property mode;
  read: { return m_mode; }
  write: { 
    if( !(__value in PHAC01_SessionMode) )
      throw "Invalid working mode value";
    m_mode = __value; }
  
  property completed;
  read: { return m_complete; }

  property completedStr;
  read: { 
    if( m_complete#isEmpty() )
      return "Not complete";
    else
      return m_complete.asString(); 
  }
  
  property fwId;
  read: { return m_bak.f_fwId; }

  property fwIdStr;
  read: { return m_bak.f_fwId$fwIdString; }
  
  property profile;
  read: { return m_profile; }
  
  property sessionInfoStr;
  read: { return EsStr::format("Session: %s, Created: %s, Complete: %s", 
    $fwIdStr, 
    m_created.asString(), 
    $completedStr ); }
}

/// Create default profile, with sample points
///
function profileDefaultCreate()
var profile = new PHAC01_CalibrationProfile("DefaultProfile"),
  collection;
{
  foreach(collection in PHAC01_CalibrationTypeIndex)
    profile.nodeAdd(collection, 10, 0.1);
  
  return profile;
}

/// Profile files manipulation
///
function profileLoad(path)
var stream = new EsStreamXml(EsStreamFlag$$Read|EsStreamFlag$$File, path);
var profile;
{
  if( stream.firstRootObjectLocate() )
  {
    profile = new PHAC01_CalibrationProfile();
    stream.objectRead(profile);
  }

  return profile;
}

function profileSave(profile, path)
var stream = new EsStreamXml(EsStreamFlag$$Write|EsStreamFlag$$File, "");
{
  if( !profile#isKindOf("PHAC01_CalibrationProfile") )
    throw "Could not write incompatible object as profile";

  if( !profile$isOk )
    throw EsStr::format("Could not write incomplete profile '%s'. Valid profile should contain at least one node for each data type",
      profile$name);
    
  stream.objectWrite(profile);  
  stream.save(path);
}

/// Calibration sessions file manipulation
///
function sessionPathGet(pathActive, idStr)
var spath = EsPath::createFromPath(pathActive);
{
  spath$fileName = EsPath::makeSafe(idStr, ". ;-", "_");
  spath$fileExt = "session";
  
  return spath;
}

function activeSessionSave(pathActive, session)
var idStr = session$fwIdStr, spath = sessionPathGet(pathActive, idStr);
var stream = new EsStreamXml(EsStreamFlag$$Write|EsStreamFlag$$File, spath.pathGet(EsPathFlag$$Default, ""));
{
  stream.objectWrite(session);
  stream.save(spath.pathGet(EsPathFlag$$Default, ""));
}

function sessionOpen(path)
var stream, session;
{
  if( EsPath::fileExists(path, "") )
  {
    session = new PHAC01_CalibrationSession();
    stream = new EsStreamXml(EsStreamFlag$$Read|EsStreamFlag$$File, path);
    if( stream.firstRootObjectLocate() )
      stream.objectRead(session);
  }
  
  return session;
}

function activeSessionLoadOrCreate(pathActive, device, profile, newMode)
var response, idStr = device$fwIdStr, stream, session;
var spath = sessionPathGet(pathActive, idStr);
{
//  EsScriptDebug::log("spath=%s", spath.pathGet(EsPathFlag$$Default, ""));
//  EsScriptDebug::log("spath exists? %s", spath.exists("") ? "yes" : "no");

  // try to find existing active session file first
  if( spath.exists("") )
  {
    session = new PHAC01_CalibrationSession();
    stream = new EsStreamXml(EsStreamFlag$$Read|EsStreamFlag$$File, spath.pathGet(EsPathFlag$$Default, ""));
    if( stream.firstRootObjectLocate() )
    {
      stream.objectRead(session);
      if( session.m_bak.f_fwId != device$fwId )
        throw EsStr::format("Settings id '%s' incompatible with device '%s'",
          session.m_bak.f_fwId$fwIdString, idStr);
      
      return session;
    }
  }
  
  // create new session in specific mode mode
  // read current settings from device, and save them in session
  response = device.settingsDownload();
  if( response[0] )
  {
    session = new PHAC01_CalibrationSession(newMode, response[1], profile);
    activeSessionSave(pathActive, session);
  }
  
  return session;
}

function activeSessionComplete(pathActive, pathComplete, session)
var spath, scpath, idStr = session$fwIdStr, stream;
{
  if( !session$isComplete )
  {
    session.m_complete = EsDateTime::now();
    session.m_mode = PHAC01_SessionMode$$Complete;
    
    spath = sessionPathGet(pathActive, idStr);
    scpath = sessionPathGet(pathComplete, idStr + " " + session.m_complete.asString());
    
    stream = new EsStreamXml(EsStreamFlag$$Write|EsStreamFlag$$File, "");
    // save to complete sessions folder
    stream.objectWrite(session);
    stream.save(scpath.pathGet(EsPathFlag$$Default, ""));
    // find and remove active session file
    if( spath.fileExists("") )
      EsPath::fileRemove(spath.pathGet(EsPathFlag$$Default, ""));
  }
}

// session file enumeration helper object
object SessionsEnumerator
{
 var m_helper, 
     m_sessionFiles;
 
 new()
 {
  m_helper = new EsPathEnumeratorHelper(this);
  m_sessionFiles = [];
 }

 function search(path)
 {
  m_sessionFiles = [];
  m_helper.enumerate(path, "*.session", EsPathEnumeratorFlag$$Default);
 }
 
 function onObject(curPath, name, isDir)
 {
  if( !isDir )
    m_sessionFiles += curPath+name;
    
  return true;
 }
 
 property sessions;
 read: { return m_sessionFiles; }
}

function activeSessionFilesGet(pathActive)
var enumerator = new SessionsEnumerator();
{
 enumerator.search(pathActive);
 return enumerator$sessions;
}

function allSessionsComplete(pathActive, pathComplete)
var sessionFiles = activeSessionFilesGet(pathActive), 
  sessionFile, session;
{
 foreach(sessionFile in sessionFiles)
 {
  session = sessionOpen(sessionFile);
  activeSessionComplete(pathActive, pathComplete, session);
 }
}

function allSessionsDelete(pathActive)
var sessionFiles = activeSessionFilesGet(pathActive), 
  sessionFile;
{
 foreach(sessionFile in sessionFiles)
   EsPath::fileRemove(sessionFile);
}
