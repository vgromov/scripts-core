// Scripting device support for pha-01 photo-appliance
##require("EsDeviceBase.ess");
##require("../math/EsSpline.ess");
##require("el3c/Consts.ess");
##require("el3c/CorMatrix.ess");
##require("el3c/FmtHelpers.ess");
##require("el3c/Legacy.ess");
##require("el3c/Flags.ess");
##require("el3c/Data.ess");
##require("el3c/CalibrationAndSettings.ess");

/// EL3C device object
object EL3C extends EsDeviceBase
@nomarshal;
{
/// Device-specific vars
  var m_opMode;
  
/// Mandatory base overrides
  function rpcNameGetById(rpcId)
  {
    if( rpcId in EL3C_Rpid )
      return EL3C_Rpid.valueLabelGet(rpcId);
    
    return EsDeviceBase::rpcNameGetById(rpcId);
  }
  
  function rpcSigGetById(rpcId)
  {
    if( rpcId in EL3C_Rpid )
      return rpcSigGet(EL3C_Rpid.valueLabelGet(rpcId));
    
    return EsDeviceBase::rpcSigGetById(rpcId);
  }
  
  function fwIdAssert(fwId)
  {
    EsDeviceBase::fwIdAssert(fwId);
    fwIdValidate(fwId, EsDeviceType$$PHAC1, "EL3C");
  }

  function fwIdDefaultSet()
  {
    m_fwId = new EsFirmwareId(EsDeviceType$$PHAC1);
    m_fwId.f_serial.f_year = EsDateTime::now()$year;
    m_fwId.f_fwVer.f_major = 1;
    m_fwId.f_fwVer.f_minor = 0;
  }
  
  function defaultCapsGet()
  var result = EsDeviceBase::defaultCapsGet() + EL3C_Rpid$values;
  {
    result += EsRpcStdId$$HEALTH_EXEC;
    result += EsRpcStdId$$HEALTH_INFO_GET;
    result += EsRpcStdId$$DATETIME_GET;
    result += EsRpcStdId$$DATETIME_SET;
    result += EsRpcStdId$$FACTORY_RESET;
    result += EsRpcStdId$$SHUTDOWN;
    result += EsRpcStdId$$ENTER_BOOT;
    result += EsRpcStdId$$POWER_STATUS_GET;
    result += EsRpcStdId$$FINDME;
    result += EsRpcStdId$$SW_INFO_GET;
    result += EsRpcStdId$$FWID_MODERN_GET;
    
    return result;
  }
  
  function rpcCallResultHandle(rpcId, rpcArgs, rpcResult)
  var result = null;
  {
    if( EsRpcStdId$$HEALTH_INFO_GET == rpcId )
      result = new EL3C_Health(rpcResult);    // use EL3C health implementation instead of unparsed standard one
    // Group all simple no-objects responses into one branch
    else if( 
      EL3C_Rpid$$WorkmodeGet == rpcId ||
      EL3C_Rpid$$BgndTmoGet == rpcId ||
      EL3C_Rpid$$AutosaveTmoGet == rpcId ||
      EL3C_Rpid$$DataCountGet == rpcId ||
      EL3C_Rpid$$LegacyOperationModeGet == rpcId ||
      EL3C_Rpid$$LegacyBgndPeriodLeftGet == rpcId ||
      EL3C_Rpid$$LegacyAutosaveIntervalGet == rpcId
      )
    {
      result = rpcResult;
      
      // in addition, save currently active device opmode
      // it's vital for live data requests handling
      if( EL3C_Rpid$$LegacyOperationModeGet == rpcId )
        m_opMode = EL3C_fromLegacyOpmode(result);
      else if( EL3C_Rpid$$WorkmodeGet == rpcId )
        m_opMode = result;
    }      
    else if( EL3C_Rpid$$CalibrationClearGet == rpcId )
    {
      result = new EL3C_CalibrationClear();
      result$buffer = rpcResult#asBinBuffer();
    }
    else if( EL3C_Rpid$$CalibrationColorGet == rpcId )
    {
      result = new EL3C_CalibrationColor();
      result$buffer = rpcResult#asBinBuffer();      
    }
    else if( EL3C_Rpid$$LiveDataGet == rpcId )
    {
      result = new EL3C_Data(); 
      result$buffer = rpcResult#asBinBuffer();
    }
    else if( EL3C_Rpid$$RawDataGet == rpcId )
    {
      result = new EL3C_RawData(); 
      result$buffer = rpcResult#asBinBuffer();
    }
    else if( EL3C_Rpid$$DataAtIdxGet == rpcId )
    {
      result = new EL3C_Data();
      result$buffer = rpcResult#asBinBuffer();
    }
    else if( EL3C_Rpid$$LegacyLiveDataGet == rpcId )
    {
      if( EL3C_LegacyOpMode$$LiveWoBgnd == EL3C_toLegacyOpmode(m_opMode) )
        result = new EL3C_LegacyDataWoBgnd();
      else
        result = new EL3C_LegacyData();
        
      result$buffer = rpcResult#asBinBuffer();
    }
    else if( EL3C_Rpid$$LegacyDataAtIdxGet == rpcId )
    {
      result = new EL3C_LegacyData();
      result$buffer = rpcResult#asBinBuffer();
    }
    else if( EL3C_Rpid$$LegacyCalibrationGet == rpcId )
    {
      result = new EL3C_LegacyCalibrationData();
      result$buffer = rpcResult#asBinBuffer();
    }
    else 
      result = EsDeviceBase::rpcCallResultHandle(rpcId, rpcArgs, rpcResult);
    
    return result;
  }
  
  /// Constructors
  new()
  {
    internalInit(null);    
    m_opMode = EL3C_OpMode$$None;
  }
  
  new(fwId)
  {
    internalInit(fwId);
    m_opMode = EL3C_OpMode$$None;
  }
  
  /// EL3C custom response handlers
  ///
  /// Download settings object
  function settingsDownload()
  var settings = new EL3C_Settings(m_fwId), rpcResult;
  {
    // Download software info
    rpcResult = rpcCall(EsRpcStdId$$SW_INFO_GET, null);
    if( rpcResult[0] )
      settings.f_swInfo.copyFrom( rpcResult[1] );
    else
      return rpcResult;
    
    rpcResult = rpcCall(EL3C_Rpid$$AutosaveTmoGet, null);
    if( rpcResult[0] )
      settings.f_autosaveInterval = rpcResult[1];
    else
      return rpcResult;
      
    rpcResult = rpcCall(EL3C_Rpid$$BgndTmoGet, null);
    if( rpcResult[0] )
      settings.f_bgndExp = rpcResult[1];
    else
      return rpcResult;      
      
    rpcResult = rpcCall(EL3C_Rpid$$CalibrationClearGet, null);
    if( rpcResult[0] )
      settings.f_calClear.copyFrom( rpcResult[1] );
    else
      return rpcResult;
    
    rpcResult = rpcCall(EL3C_Rpid$$CalibrationColorGet, null);
    if( rpcResult[0] )
      settings.f_calColor.copyFrom( rpcResult[1] );
    else
      return rpcResult;
    
    rpcResult = [];
    rpcResult += true;
    rpcResult += settings;
    
    return rpcResult;
  }
  
  /// Upload settings object
  function settingsUpload(settings)
  var batch = [], batchItem = [];
  {
    if( settings.f_fwId != m_fwId )
      throw "Could not upload incompatible device settings";
    
    settings.crcUpdate();
    
    batchItem += EL3C_Rpid$$AutosaveTmoSet;
    batchItem += settings.f_autosaveInterval;
    batch#append(batchItem);
    batchItem = [];
    
    batchItem += EL3C_Rpid$$BgndTmoSet;
    batchItem += settings.f_bgndExp;
    batch#append(batchItem);
    batchItem = [];
        
    batchItem += EL3C_Rpid$$CalibrationClearSet;
    batchItem += settings.f_calClear;
    batch#append(batchItem);
    batchItem = [];
    //EsScriptDebug::log("Clear calibration size: %d", settings.f_calClear$size);
    
    batchItem += EL3C_Rpid$$CalibrationColorSet;
    batchItem += settings.f_calColor;
    batch#append(batchItem);
    batchItem = [];
    
    batch = rpcBatchCall(batch);
    //EsScriptDebug::log(batch);
    
    // We're not interested in returning entire batch result.
    // just treat batch results as a whole.
    if( deviceCheckBatchResultOk(batch) )
    {
      batchItem += true;
      batchItem += null; // nothing is returned
    }
    else
    {
      batchItem += false;
      batchItem += "Device settings upload fail";
    }
    batch = [];
    batch#append(batchItem);
    
    return batch;
  }
  
  /// Properties
  ///
  /// Currently active opmode
  property opmode;
  read: { return m_opMode; }
  
  /// Opmode string
  property opmodeStr;
  read:
  var helper = new EL3C_DataFlags(m_opMode);
  { 
    return m_opMode;
  }
}
